/*  Copyright 2011-2013 Alexis Herault, Giuseppe Bilotta, Robert A. Dalrymple, Eugenio Rustico, Ciro Del Negro

    Istituto Nazionale di Geofisica e Vulcanologia
        Sezione di Catania, Catania, Italy

    Università di Catania, Catania, Italy

    Johns Hopkins University, Baltimore, MD

    This file is part of GPUSPH.

    GPUSPH is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    GPUSPH is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with GPUSPH.  If not, see <http://www.gnu.org/licenses/>.
*/

#include <vector_types.h>

#ifdef XSPH_KERNEL
#	ifdef DT_KERNEL
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE, Xsph, Dt)
#	else
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE, Xsph,)
#	endif
#else
#	ifdef DT_KERNEL
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE,, Dt)
#	else
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE,,)
#	endif
#endif

/************************************************************************************************************/
/*		   Kernels for computing forces with the different options											*/
/************************************************************************************************************/
template<KernelType kerneltype, BoundaryType boundarytype, bool usedem,
		 SPHFormulation sph_formulation>
__global__ void
//__launch_bounds__(BLOCK_SIZE_FORCES, MIN_BLOCKS_FORCES)
FORCES_CUDA_KERNEL
(	
	const float4*	posArray,
	float4*		forces,
	float2*		keps_dkde,
	float*		turbvisc,
	const hashKey*	particleHash,
	const uint*	cellStart,
	# ifdef XSPH_KERNEL
	float4*		xsph,
	# endif
	const neibdata*	neibsList,
	const uint	numParticles,
	const float deltap,
	const float	slength,
	const float	influenceradius,
	float4*		rbforces,
	float4*		rbtorques
	# ifdef DT_KERNEL
	,
	float*		cfl,
	float*		cflGamma,
	float*		cfltvisc
	# endif
)
{
	// Global particle index
	const uint index = INTMUL(blockIdx.x,blockDim.x) + threadIdx.x;
	# ifdef DT_KERNEL
	__shared__ float sm_max[BLOCK_SIZE_FORCES];
	sm_max[threadIdx.x] = 0.0f;
	__shared__ float sm_max_g[BLOCK_SIZE_FORCES];
	sm_max_g[threadIdx.x] = 0.0f;
	# ifdef VISC_KEPS
	__shared__ float sm_max_nut[BLOCK_SIZE_FORCES];
	sm_max_nut[threadIdx.x] = 0.0f;
	# endif
	# endif

	if (index < numParticles) {
		// read particle info from texture
		const particleinfo info = tex1Dfetch(infoTex, index);

		uint cellHash = cellHashFromParticleHash(particleHash[index]);

		// Compute force only if the given particle if a fluid one
		if (FLUID(info)) {

			// Read other particle datas from textures or global mem
			#if( __COMPUTE__ >= 20)
			const float4 pos = posArray[index];
			#else
			const float4 pos = tex1Dfetch(posTex, index);
			#endif

			const float4 vel = tex1Dfetch(velTex, index);
			const float4 gradgamma = (boundarytype == SA_BOUNDARY ? tex1Dfetch(gamTex, index) : make_float4(NAN));

			// Compute grid position of current particle
			const int3 gridPos = calcGridPosFromHash(cellHash);

			# ifdef VISC_SPS
			// read particle SPS matrix
			symtensor3 tau;
			float2 temp = tex1Dfetch(tau0Tex, index);
			tau.xx = temp.x;
			tau.xy = temp.y;
			temp = tex1Dfetch(tau1Tex, index);
			tau.xz = temp.x;
			tau.yy = temp.y;
			temp = tex1Dfetch(tau2Tex, index);
			tau.yz = temp.x;
			tau.zz = temp.y;
			# endif

			# ifdef VISC_KEPS
			const float strain = tex1Dfetch(strainTex, index);
			const float keps_k = tex1Dfetch(keps_kTex, index);
			const float keps_e = tex1Dfetch(keps_eTex, index);
			const float turbVisc = 0.09f*keps_k*keps_k/keps_e;			// eddy viscosity
			float Pturb = turbVisc*strain*strain;						// production of turbulent kinetic energy (TKE)
			turbvisc[index] = turbVisc;

			float diff_term_k = 0;
			float diff_term_e = 0;
			const float dkdt_precalc = vel.w*(d_visccoeff + turbVisc);
			const float dedt_precalc = vel.w*(d_visccoeff + turbVisc/1.3f);
			# endif

			// force and density derivative
			float4 force = make_float4(0.0f);

			# ifdef XSPH_KERNEL
			// XSPH correction
			float3 mean_vel = make_float3(0.0f);
			# endif

			/* precomputed component of DvDt */
			float p_precalc = 0;
			switch (sph_formulation) {
				/* when using SPH formulation 1
				   we precompute p/rho^2 for the current particle */
				case SPH_F1:
					p_precalc = P(vel.w, PART_FLUID_NUM(info))/(vel.w*vel.w);
					//in case of k-e model we use p~ = p + 2/3*rho*k
					# ifdef VISC_KEPS
					p_precalc += 2*keps_k/vel.w/3.f;
					# endif
					break;
				/* when using SPH formulation 2
				   we precompute the pressure for the current particle */
				case SPH_F2:
					p_precalc = P(vel.w, PART_FLUID_NUM(info));
					break;
			}

			//FIXME: Speed of sound should be calculated with SA_BOUNDARY, even when dt is constant
			//# if defined(DT_KERNEL) || defined(VISC_ARTVISC)
			/* sound speed sspeed is computed only once for the given particle */
			const float sspeed = soundSpeed(vel.w, PART_FLUID_NUM(info));
			//# endif

			# ifdef DT_KERNEL
			float gradgamU = 0;
			# endif

			// persistent variables across getNeibData calls
			char neib_cellnum = 0;
			uint neib_cell_base_index = 0;

			// loop over all neighbors
			for (idx_t i = 0; i < d_neiblist_end; i += d_neiblist_stride) {
				neibdata neib_data = neibsList[i + index];

				if (neib_data == 0xffff) break;

				float3 pos_corr;
				const uint neib_index = getNeibIndex(pos, pos_corr, cellStart, neib_data, gridPos,
													neib_cellnum, neib_cell_base_index);

				// Compute relative position vector and distance
				// Now relPos is a float4 and neib mass is stored in relPos.w
				#if( __COMPUTE__ >= 20)
				const float4 relPos = pos_corr - posArray[neib_index];
				#else
				const float4 relPos = pos_corr - tex1Dfetch(posTex, neib_index);
				#endif

				// skip inactive particles
				if (INACTIVE(relPos))
					continue;

				const float r = length(as_float3(relPos));

				// Compute relative velocity
				// Now relVel is a float4 and neib density is stored in relVel.w
				const float4 relVel = as_float3(vel) - tex1Dfetch(velTex, neib_index);
				const particleinfo neib_info = tex1Dfetch(infoTex, neib_index);
				# ifdef VISC_KEPS
				const float neib_keps_k = tex1Dfetch(keps_kTex, neib_index);
				const float neib_keps_e = tex1Dfetch(keps_eTex, neib_index);
				const float neib_tvisc = 0.09f*neib_keps_k*neib_keps_k/neib_keps_e;
				# endif

				if (r < influenceradius) {
					float DvDt = 0.0f;
					float4 bound_term_pres = make_float4(0.0f);
					float3 bound_term_visc = make_float3(0.0f);

					if (FLUID(neib_info) || (boundarytype == SA_BOUNDARY && (BOUNDARY(neib_info) || VERTEX(neib_info)) ) ) {
						const float4 belem = (boundarytype == SA_BOUNDARY ? tex1Dfetch(boundTex, neib_index) : make_float4(NAN));
						const float neib_sspeed = soundSpeed(relVel.w, PART_FLUID_NUM(neib_info));

						/* TODO-AM should be removed I suppose
						float neib_pres = tex1Dfetch(presTex, neib_index);
						if(FLUID(neib_info))
							neib_pres = P(relVel.w, PART_FLUID_NUM(neib_info)); */

						# ifdef VISC_SPS
						symtensor3 neib_tau;
						temp = tex1Dfetch(tau0Tex, neib_index);
						neib_tau.xx = temp.x;
						neib_tau.xy = temp.y;
						temp = tex1Dfetch(tau1Tex, neib_index);
						neib_tau.xz = temp.x;
						neib_tau.yy = temp.y;
						temp = tex1Dfetch(tau2Tex, neib_index);
						neib_tau.yz = temp.x;
						neib_tau.zz = temp.y;
						# endif

						const float vel_dot_pos = dot(as_float3(relVel), as_float3(relPos));	// x.u
						const float f = F<kerneltype>(r, slength);		// 1/r dW/dr

						// density derivative
						float DrDt = 0;
						if (boundarytype == SA_BOUNDARY && BOUNDARY(neib_info))
							DrDt -= relVel.w*dot(as_float3(relVel), as_float3(gradGamma<kerneltype>(slength, r, belem)) );
						else if (boundarytype == SA_BOUNDARY && d_ferrari) {
							const float grav_corr = -dot(d_gravity, as_float3(relPos))*d_rho0[PART_FLUID_NUM(info)]/ 
								(d_sscoeff[PART_FLUID_NUM(info)]*d_sscoeff[PART_FLUID_NUM(info)]); //TODO-AM this does not work with multifluids
							float3 ferraricor = (r > 1e-4*slength) ? max(sspeed, neib_sspeed)*(vel.w - relVel.w + grav_corr)/vel.w/r*as_float3(relPos) : make_float3(0.0);
							DrDt = relPos.w*dot(as_float3(relVel) + d_ferrari*ferraricor, as_float3(relPos))*f;
						}
						else
							DrDt = relPos.w*vel_dot_pos*f;
						/* The second formulation takes into consideration the density ratio */
						if (sph_formulation == SPH_F2)
							DrDt *= vel.w/relVel.w;
						if(boundarytype == SA_BOUNDARY) {
							DrDt /= gradgamma.w;
						}
						force.w += DrDt;

						// pressure part of acceleration (to be multiplied by neib_mass*f)
						switch (sph_formulation) {
							case SPH_F1:
								DvDt = -(p_precalc + P(relVel.w, PART_FLUID_NUM(neib_info))/(relVel.w*relVel.w)); /*I think these are i not index*/
								# ifdef VISC_KEPS
								DvDt -= 2*neib_keps_k/relVel.w/3.f;
								# endif
								break;
							case SPH_F2:
								DvDt = -(p_precalc + P(relVel.w, PART_FLUID_NUM(neib_info)))/(vel.w*relVel.w);
								break;
						}

						/* Viscous forces */
						# if defined(VISC_DYNAMIC) || defined(VISC_KINEMATIC) || defined(VISC_SPS) || defined(VISC_KEPS)
						# if defined(VISC_DYNAMIC)
						const float visc = laminarvisc_dynamic(vel.w, relVel.w, relPos.w, f, d_visccoeff*vel.w,
										d_visccoeff*relVel.w);
						# elif defined(VISC_KINEMATIC)
						const float visc = laminarvisc_kinematic(vel.w, relVel.w, relPos.w, f);
						# elif defined(VISC_SPS)
						const float visc = laminarvisc_kinematic(vel.w, relVel.w, relPos.w, f);
						force.x += relPos.w*f*((tau.xx + neib_tau.xx)*relPos.x + (tau.xy + neib_tau.xy)*relPos.y +
										(tau.xz + neib_tau.xz)*relPos.z);
						force.y += relPos.w*f*((tau.xy + neib_tau.xy)*relPos.x + (tau.yy + neib_tau.yy)*relPos.y +
										(tau.yz + neib_tau.yz)*relPos.z);
						force.z += relPos.w*f*((tau.xz + neib_tau.xz)*relPos.x + (tau.yz + neib_tau.yz)*relPos.y +
										(tau.zz + neib_tau.zz)*relPos.z);
						# elif defined(VISC_KEPS)
						const float visc = laminarvisc_dynamic(vel.w, relVel.w, relPos.w, f, (d_visccoeff+turbVisc)*vel.w,
										(d_visccoeff+neib_tvisc)*relVel.w);
						# endif // VISC_DYNAMIC + elif*

						if(!BOUNDARY(neib_info)) {
							if(boundarytype == SA_BOUNDARY) {
								as_float3(force) += visc*as_float3(relVel)/gradgamma.w;
							}
							else {
								as_float3(force) += visc*as_float3(relVel);
							}
						}

						# elif defined(VISC_ARTVISC)
						if (vel_dot_pos < 0.0f)
							DvDt += artvisc(vel_dot_pos, vel.w, relVel.w, sspeed,
										soundSpeed(relVel.w, PART_FLUID_NUM(neib_info)), r, slength);
						# else
						# error Unknown viscosity!
						# endif // VISC_*

						# ifdef XSPH_KERNEL
						mean_vel -= relPos.w*W<kerneltype>(r, slength)*as_float3(relVel)/(vel.w + relVel.w);
						# endif

						if(boundarytype == SA_BOUNDARY) {
							if(BOUNDARY(neib_info)) {
								const float4 gradgam_as = gradGamma<kerneltype>(slength, r, belem);
								const float abs_gradgam_as = length(as_float3(gradgam_as));
								bound_term_pres = DvDt*relVel.w*gradgam_as;

								// normal vector
								const float3 normal_s = as_float3(belem);
								// r_as - distance between fluid particle and boundary element along the normal
								float r_as = dot(as_float3(relPos), normal_s);
								if (r_as < deltap)
									r_as = deltap;

								// velocity of fluid particle along the wall
								const float3 vel_tau = as_float3(relVel) - dot(as_float3(relVel), normal_s)*normal_s;

								// These two expressions are correct only when used together with DYNAMICVISC or KEPSVISC model
								# ifdef VISC_KEPS
								// a component of fluid paricle velocity tangential to the wall
								const float3 u_t = as_float3(vel) - dot(as_float3(vel), normal_s)*normal_s;
								const float abs_u_t = length(u_t);
								float y_plus = deltap;

								// we solve iteratively the wall law equation to obtain y+ value
								float u_star = exp(-2.132f)*d_visccoeff/r_as;
								for (int i=0; i<10; i++) {
									y_plus = max(r_as*u_star/d_visccoeff, 11.f);
									u_star = (0.41f*abs_u_t + u_star)/(log(y_plus) + 2.132f + 1);
								}
								y_plus = max(r_as*u_star/d_visccoeff, 11.f);

								const float denom_term = pow(log(y_plus)/0.41f + 5.2f, 2);
								bound_term_visc = 2*vel.w*abs_u_t*abs_gradgam_as*u_t/denom_term;
								# else
								bound_term_visc = length(gradgam_as)*d_visccoeff*(vel.w + relVel.w)/r_as*vel_tau;
								# endif

								# ifdef VISC_KEPS
								//to be divided later by rho_a and gamma_a
								diff_term_e += pow(0.09f, 0.75f)/0.41f * (dedt_precalc*pow(keps_k,1.5f)/(r_as*r_as) +
											relVel.w*(d_visccoeff + neib_tvisc/1.3f)*pow(neib_keps_k,1.5f)/(deltap*deltap)) * abs_gradgam_as;
								# endif

								# ifdef DT_KERNEL
								float tmp = abs(dot(as_float3(gradgam_as), as_float3(vel)));
								if (tmp > gradgamU)
									gradgamU = tmp;
								# endif
							}
							# ifdef VISC_KEPS
							else {
								//to be divided later by rho_a and gamma_a
								diff_term_k += relPos.w*(dkdt_precalc + relVel.w*(d_visccoeff + neib_tvisc))*(keps_k - neib_keps_k)*f/relVel.w;
								diff_term_e += relPos.w*(dedt_precalc + relVel.w*(d_visccoeff + neib_tvisc/1.3f))*(keps_e - neib_keps_e)*f/relVel.w;
							}
							# endif
						} // endif (boundarytype)

						DvDt *= relPos.w*f;
					} else
					switch (boundarytype) {
						case LJ_BOUNDARY:
							DvDt = LJForce(r);
							break;
						case MK_BOUNDARY:
							DvDt = MKForce(r, slength, pos.w, pos.w);
							break;
						case SA_BOUNDARY:
							break;
						default:
							DvDt = 1.0f/0.0f;
							break;
					}
					// interaction between two particles
					if(boundarytype == SA_BOUNDARY) {
						if(BOUNDARY(neib_info)) {
							as_float3(force) -= as_float3(bound_term_pres)/gradgamma.w + bound_term_visc/gradgamma.w/vel.w;
						}
						else {
							as_float3(force) += DvDt*as_float3(relPos)/gradgamma.w;
						}
					}
					else {
						as_float3(force) += DvDt*as_float3(relPos);
					}

				} // r < influenceradius
			} // end of loop over neighbors

			# ifdef VISC_DYNAMIC
			const float dynvisc = d_visccoeff*vel.w;
			# elif defined(VISC_KINEMATIC) || defined(VISC_SPS)
			const float dynvisc = d_visccoeff*vel.w/4.0f;				// FXME: check????????
			# elif defined(VISC_ARTVISC)
			const float dynvisc = 0.0f;
			# elif defined(VISC_KEPS)
			const float dynvisc = 0.0f;

			float dkdt = Pturb - keps_e + diff_term_k/vel.w/gradgamma.w;								// dk/dt
			float dedt = keps_e/keps_k*(1.44f*Pturb - 1.92f*keps_e) + diff_term_e/vel.w/gradgamma.w;	// de/dt

			keps_dkde[index].x = dkdt;
			keps_dkde[index].y = dedt;
			# else
			# error Unknown viscosity!
			# endif

			// Adding gravity
			as_float3(force) += d_gravity;

			// TODO: check for time step limitation in case of geometrical boundaries (DEM or planes)
			// for viscous fluids
			float geom_coeff = 0.0f;

			// Adding repulsive force computed from DEM
			if (usedem) {
				switch (boundarytype) {
					case LJ_BOUNDARY:
						geom_coeff = DemLJForce(demTex, as_float3(pos) + gridPos*d_cellSize + 0.5f*d_cellSize,
										pos.w, as_float3(vel), dynvisc, force);
						break;
					default:
						break;
				}
			}

			// Adding repulsive force computed from geometric boundaries
			if (d_numplanes) {
				geom_coeff = max(geom_coeff, GeometryForce(d_worldOrigin + as_float3(pos) + gridPos*d_cellSize + 0.5f*d_cellSize,
							pos.w, as_float3(vel), dynvisc, force));
			}

			# ifdef DT_KERNEL
			// storing data for cfl condition in shared memory			
			sm_max[threadIdx.x] = max(length(as_float3(force)), sspeed*sspeed/slength);
			if(boundarytype == SA_BOUNDARY)
				sm_max_g[threadIdx.x] = gradgamU;
			# ifdef VISC_KEPS
			sm_max_nut[threadIdx.x] = turbVisc;
			# endif
			# endif

			# ifdef XSPH_KERNEL
			xsph[index] = make_float4(2.0f*mean_vel, 0.0f);
			# endif

			forces[index] = force;
		}	// if (FLUID(info))

		else if (boundarytype == SA_BOUNDARY && VERTEX(info)) {
			float4 force = make_float4(0.0f);

			// It is suggested that k-e model is working only with semi-analytical boundary model.
			// Add addtional condition on boundary_type if it isn't.
			# ifdef VISC_KEPS

			// read other particle data from textures or global mem
			#if( __COMPUTE__ >= 20)
			const float4 pos = posArray[index];
			#else
			const float4 pos = tex1Dfetch(posTex, index);
			#endif

			// compute grid index of current particle
			const int3 gridPos = calcGridPosFromHash(cellHash);

			const float4 vel = tex1Dfetch(velTex, index);
			const float4 gradgamma = tex1Dfetch(gamTex, index);

			# ifdef VISC_KEPS
			const float keps_k = tex1Dfetch(keps_kTex, index);
			const float keps_e = tex1Dfetch(keps_eTex, index);
			const float turbVisc = 0.09f*keps_k*keps_k/keps_e;			// eddy viscosity
			# endif

			// persistent variables across getNeibData calls
			char neib_cellnum = 0;
			uint neib_cell_base_index = 0;

			// loop over all neighbors
			for (idx_t i = 0; i < d_neiblist_end; i += d_neiblist_stride) {
				neibdata neib_data = neibsList[i + index];

				if (neib_data == 0xffff) break;

				float3 pos_corr;
				const uint neib_index = getNeibIndex(pos, pos_corr, cellStart, neib_data, gridPos,
							neib_cellnum, neib_cell_base_index);

				// Compute relative position vector and distance
				// Now relPos is a float4 and neib mass is stored in relPos.w
				#if( __COMPUTE__ >= 20)
				const float4 relPos = pos_corr - posArray[neib_index];
				#else
				const float4 relPos = pos_corr - tex1Dfetch(posTex, neib_index);
				#endif
				const float r = length(as_float3(relPos));

				const float4 relVel = as_float3(vel) - tex1Dfetch(velTex, neib_index);
				const particleinfo neib_info = tex1Dfetch(infoTex, neib_index);
				const float neib_keps_k = tex1Dfetch(keps_kTex, neib_index);
				const float neib_keps_e = tex1Dfetch(keps_eTex, neib_index);
				const float neib_tvisc = 0.09f*neib_keps_k*neib_keps_k/neib_keps_e;

				if (r < influenceradius) {
					if (BOUNDARY(neib_info)) {
						const float4 belem = tex1Dfetch(boundTex, neib_index);
						const float3 normal_s = as_float3(belem);	// normal vector
						// r_es - distance between vertex particle and boundary element along the normal
						float r_es = dot(as_float3(relPos), normal_s);
						if (r_es < deltap)
							r_es = deltap;

						const float abs_gradgam_es = length(gradGamma<kerneltype>(slength, r, belem));

						// velocity of vertex particle along the wall
						const float3 u_t = as_float3(vel) - dot(as_float3(vel), normal_s)*normal_s;
						const float abs_u_t = length(u_t);
						float y_plus = deltap;

						// we solve iteratively the wall law equation to obtain y+ value
						float u_star = exp(-2.132f)*d_visccoeff/r_es;
						for (int i=0; i<10; i++) {
							y_plus = max(r_es*u_star/d_visccoeff, 11.f);
							u_star = (0.41f*abs_u_t + u_star)/(log(y_plus) + 2.132f + 1);
						}
						y_plus = max(r_es*u_star/d_visccoeff, 11.f);

						const float denom_term = pow(log(y_plus)/0.41f + 5.2f, 2)*gradgamma.w;

						force.x -= 2*abs_u_t*abs_gradgam_es*u_t.x/denom_term;
						force.y -= 2*abs_u_t*abs_gradgam_es*u_t.y/denom_term;
						force.z -= 2*abs_u_t*abs_gradgam_es*u_t.z/denom_term;
					}
					else {
						const float vel_dot_pos = dot(as_float3(relVel), as_float3(relPos));	// x.u
						const float f = F<kerneltype>(r, slength);		// 1/r dW/dr
						const float visc = laminarvisc_dynamic(vel.w, relVel.w, relPos.w, f, (d_visccoeff+turbVisc)*vel.w,
										(d_visccoeff+neib_tvisc)*relVel.w);

						force.x += visc*relVel.x/gradgamma.w;
						force.y += visc*relVel.y/gradgamma.w;
						force.z += visc*relVel.z/gradgamma.w;
					} //end if BOUNDARY(neib_info)
				} // r < influenceradius
			} // end of loop over neighbors

			# endif

			forces[index] = force;
		}

		else if (OBJECT(info)) {
			float4 force = make_float4(0.0f);

			// read other particle data from textures or global mem
			#if( __COMPUTE__ >= 20)
			const float4 pos = posArray[index];
			#else
			const float4 pos = tex1Dfetch(posTex, index);
			#endif

			// compute grid index of current particle
			const int3 gridPos = calcGridPosFromHash(cellHash);

			// persistent variables across getNeibData calls
			char neib_cellnum = 0;
			uint neib_cell_base_index = 0;

			// loop over all neighbors
			for (idx_t i = 0; i < d_neiblist_end; i += d_neiblist_stride) {
				neibdata neib_data = neibsList[i + index];

				if (neib_data == 0xffff) break;

				float3 pos_corr;
				const uint neib_index = getNeibIndex(pos, pos_corr, cellStart, neib_data, gridPos,
							neib_cellnum, neib_cell_base_index);

				// Compute relative position vector and distance
				// Now relPos is a float4 and neib mass is stored in relPos.w
				#if( __COMPUTE__ >= 20)
				const float4 relPos = pos_corr - posArray[neib_index];
				#else
				const float4 relPos = pos_corr - tex1Dfetch(posTex, neib_index);
				#endif
				const float r = length(as_float3(relPos));

				const particleinfo neib_info = tex1Dfetch(infoTex, neib_index);

				if (r < influenceradius) {
					float DvDt = 0.0f;

					// Now object-object and object boundary forces are computed with ODE
					if (FLUID(neib_info) && !OBJECT(neib_info))
						DvDt = relPos.w*LJForce(r);

					as_float3(force) += DvDt*as_float3(relPos);
				}
			}

			uint rbindex = id(info) + d_rbstartindex[object(info)];
			rbforces[rbindex] = force;
			rbtorques[rbindex] = make_float4(cross(as_float3(pos) + gridPos*d_cellSize + 0.5f*d_cellSize
								- d_rbcg[object(info)], as_float3(force)));
		} // else if (OBJECT(info))
	}	// if index < numParticles)

	# ifdef DT_KERNEL
	dtadaptBlockReduce(sm_max, cfl);
	if(boundarytype == SA_BOUNDARY)
		dtadaptBlockReduce(sm_max_g, cflGamma);
	# ifdef VISC_KEPS
	dtadaptBlockReduce(sm_max_nut, cfltvisc);
	# endif
	# endif
}
/************************************************************************************************************/

#undef FORCES_CUDA_KERNEL

/* vi:set ft=cuda: */
