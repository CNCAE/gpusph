/*  Copyright 2011 Alexis Herault, Giuseppe Bilotta, Robert A. Dalrymple, Eugenio Rustico, Ciro Del Negro

	Istituto de Nazionale di Geofisica e Vulcanologia
          Sezione di Catania, Catania, Italy

    Universita di Catania, Catania, Italy

    Johns Hopkins University, Baltimore, MD

  Â  This file is part of GPUSPH.

    GPUSPH is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    GPUSPH is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with GPUSPH.  If not, see <http://www.gnu.org/licenses/>.
*/

#include <vector_types.h>

#ifdef XSPH_KERNEL
#	ifdef DT_KERNEL
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE, Xsph, Dt)
#	else
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE, Xsph,)
#	endif
#else
#	ifdef DT_KERNEL
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE,, Dt)
#	else
#		define FORCES_CUDA_KERNEL FORCES_KERNEL_NAME(VISC_TYPE,,)
#	endif
#endif

#define CELLNUMENCODED		(1U<<11)
#define NEIBINDEXMASK		(0x7FF)

/************************************************************************************************************/
/*		   Kernels for computing forces with the different options											*/
/************************************************************************************************************/
template<KernelType kerneltype, BoundaryType boundarytype, bool usedem,
		 SPHFormulation sph_formulation>
__global__ void
//__launch_bounds__(BLOCK_SIZE_FORCES, MIN_BLOCKS_FORCES)
FORCES_CUDA_KERNEL
(	
	const float4*	posArray,
	float4*		forces,
	const uint*	particleHash,
	const uint*	cellStart,
	# ifdef XSPH_KERNEL
	float4*		xsph,
	# endif
	const neibdata*	neibsList,
	const uint	numParticles,
	const float	slength,
	const float	influenceradius,
	float4*		rbforces,
	float4*		rbtorques
	# ifdef DT_KERNEL
	,
	float*		cfl
	# endif
)
{
	// Global particle index
	const uint index = INTMUL(blockIdx.x,blockDim.x) + threadIdx.x;
	# ifdef DT_KERNEL
	__shared__ float sm_max[BLOCK_SIZE_FORCES];
	sm_max[threadIdx.x] = 0.0f;
	# endif

	if (index < numParticles) {
		// Read particle info from texture
    	const particleinfo info = tex1Dfetch(infoTex, index);

		// Compute force only if the given particle if a fluid one
		if (FLUID(info)) {

			// Read other particle datas from textures or global mem
			#if( __COMPUTE__ >= 20)
			const float4 pos = posArray[index];
			#else
			const float4 pos = tex1Dfetch(posTex, index);
			#endif
			const float4 vel = tex1Dfetch(velTex, index);

			// Compute grid position of current particle
			const int3 gridPos = calcGridPosFromHash(particleHash[index]);

			# ifdef VISC_SPS
			// read particle SPS matrix
			sym33mat tau;
			float2 temp = tex1Dfetch(tau0Tex, index);
			tau.a11 = temp.x;
			tau.a12 = temp.y;
			temp = tex1Dfetch(tau1Tex, index);
			tau.a13 = temp.x;
			tau.a22 = temp.y;
			temp = tex1Dfetch(tau2Tex, index);
			tau.a23 = temp.x;
			tau.a33 = temp.y;
			# endif
			// force and density derivative
			float4 force = make_float4(0.0f);

			# ifdef XSPH_KERNEL
			// XSPH correction
			float3 mean_vel = make_float3(0.0f);
			# endif

			/* precomputed component of DvDt */
			float p_precalc = 0;
			switch (sph_formulation) {
				/* when using SPH formulation 1
				   we precompute p/rho^2 for the current particle */
				case SPH_F1:
					p_precalc = P(vel.w, PART_FLUID_NUM(info))/(vel.w*vel.w);                      //
					break;
				/* when using SPH formulation 2
				   we precompute the pressure for the current particle */
				case SPH_F2:
					p_precalc = P(vel.w, PART_FLUID_NUM(info));                                    //
					break;
			}

			# if defined(DT_KERNEL) || defined(VISC_ARTVISC)
			/* sound speed sspeed is computed only once for the given particle */
			const float sspeed = soundSpeed(vel.w, PART_FLUID_NUM(info));
			# endif

			// persistent variables across getNeibData calls
			char neib_cellnum = 0;
			uint neib_cell_base_index = 0;

			// loop over all neighbors
			for(uint i = 0; i < d_maxneibsnum_time_numparticles; i += numParticles) {
				neibdata neib_data = neibsList[i + index];

				if (neib_data == 0xffff) break;

				float3 pos_corr;
				const uint neib_index = getNeibIndex(pos, pos_corr, cellStart, neib_data, gridPos,
													neib_cellnum, neib_cell_base_index);

				// Compute relative position vector and distance
				// Now relPos is a float4 and neib mass is stored in relPos.w
				#if( __COMPUTE__ >= 20)
				const float4 relPos = pos_corr - posArray[neib_index];
				#else
				const float4 relPos = pos_corr - tex1Dfetch(posTex, neib_index);
				#endif
				const float r = length(as_float3(relPos));

				// Compute relative velocity
				// Now relVel is a float4 and neib density is stored in relVel.w
				const float4 relVel = as_float3(vel) - tex1Dfetch(velTex, neib_index);
    	        const particleinfo neib_info = tex1Dfetch(infoTex, neib_index);	

				if (r < influenceradius) {
					float DvDt = 0.0f;

					if (FLUID(neib_info)) {

						# ifdef VISC_SPS
						sym33mat neib_tau;
						temp = tex1Dfetch(tau0Tex, neib_index);
						neib_tau.a11 = temp.x;
						neib_tau.a12 = temp.y;
						temp = tex1Dfetch(tau1Tex, neib_index);
						neib_tau.a13 = temp.x;
						neib_tau.a22 = temp.y;
						temp = tex1Dfetch(tau2Tex, neib_index);
						neib_tau.a23 = temp.x;
						neib_tau.a33 = temp.y;
						# endif

						const float vel_dot_pos = dot(as_float3(relVel), as_float3(relPos));	// x.u
						const float f = F<kerneltype>(r, slength);		// 1/r dW/dr

						// density derivative
						float DrDt = relPos.w*vel_dot_pos*f;
						/* The second formulation takes into consideration the density ratio */
						if (sph_formulation == SPH_F2)
							DrDt *= vel.w/relVel.w;
						force.w += DrDt;

						// pressure part of acceleration (to be multiplied by neib_mass*f)
						switch (sph_formulation) {
							case SPH_F1:
								DvDt = -(p_precalc + P(relVel.w, PART_FLUID_NUM(neib_info))/(relVel.w*relVel.w)); /*I think these are i not index*/
								break;
							case SPH_F2:
								DvDt = -(p_precalc + P(relVel.w, PART_FLUID_NUM(neib_info)))/(vel.w*relVel.w);
								break;
						}

						/* Viscous forces */
						# if defined(VISC_DYNAMIC) || defined(VISC_KINEMATIC) || defined(VISC_SPS)
						# if defined(VISC_DYNAMIC)
						const float visc = laminarvisc_dynamic(vel.w, relVel.w, relPos.w, f, d_visccoeff*vel.w,
										d_visccoeff*relVel.w);
						# elif defined(VISC_KINEMATIC)
						const float visc = laminarvisc_kinematic(vel.w, relVel.w, relPos.w, f);
						# elif defined(VISC_SPS)
						const float visc = laminarvisc_kinematic(vel.w, relVel.w, relPos.w, f);
						force.x += relPos.w*f*((tau.a11 + neib_tau.a11)*relPos.x + (tau.a12 + neib_tau.a12)*relPos.y +
										(tau.a13 + neib_tau.a13)*relPos.z);
						force.y += relPos.w*f*((tau.a12 + neib_tau.a12)*relPos.x + (tau.a22 + neib_tau.a22)*relPos.y +
										(tau.a23 + neib_tau.a23)*relPos.z);
						force.z += relPos.w*f*((tau.a13 + neib_tau.a13)*relPos.x + (tau.a23 + neib_tau.a23)*relPos.y +
										(tau.a33 + neib_tau.a33)*relPos.z);
						# endif // VISC_DYNAMIC + elif*

						as_float3(force) += visc*as_float3(relVel);

						# elif defined(VISC_ARTVISC)
						if (vel_dot_pos < 0.0f)
							DvDt += artvisc(vel_dot_pos, vel.w, relVel.w, sspeed,
										soundSpeed(relVel.w, PART_FLUID_NUM(neib_info)), r, slength);
						# else
						# error Unknown viscosity!
						# endif // VISC_*

						# ifdef XSPH_KERNEL
						mean_vel -= relPos.w*W<kerneltype>(r, slength)*as_float3(relVel)/(vel.w + relVel.w);
						# endif

						DvDt *= relPos.w*f;
					} else
					switch (boundarytype) {
						case LJ_BOUNDARY:
							DvDt = LJForce(r);
							break;
						case MK_BOUNDARY:
							DvDt = MKForce(r, slength, pos.w, pos.w);
							break;
						default:
							DvDt = 1.0/0.0;
							break;
					}
					// interaction between two particles
					as_float3(force) += DvDt*as_float3(relPos);

				} // r < influenceradius
			} // end of loop over neighbors

			# ifdef VISC_DYNAMIC
			const float dynvisc = d_visccoeff*vel.w;
			# elif defined(VISC_KINEMATIC) || defined(VISC_SPS)
			const float dynvisc = d_visccoeff*vel.w/4.0f;				// FXME: check????????
			# elif defined(VISC_ARTVISC)
			const float dynvisc = 0.0;
			# else
			# error Unknown viscosity!
			# endif

			// Adding gravity
			as_float3(force) += d_gravity;

			// TODO: check for time step limitation in case of geometrical boundaries (DEM or planes)
			// for viscous fluids
			float geom_coeff = 0.0f;

			// Adding repulsive force computed from DEM
			if (usedem) {
				switch (boundarytype) {
					case LJ_BOUNDARY:
						geom_coeff = DemLJForce(demTex, as_float3(pos) + gridPos*d_cellSize + 0.5f*d_cellSize,
										pos.w, as_float3(vel), dynvisc, force);
						break;
					default:
						break;
				}
			}

			// Adding repulsive force computed from geometric boundaries
			if (d_numplanes) {
				geom_coeff = max(geom_coeff, GeometryForce(as_float3(pos) + gridPos*d_cellSize + 0.5f*d_cellSize,
							pos.w, as_float3(vel), dynvisc, force));
			}
			
			# ifdef DT_KERNEL
			// storing data for cfl condition in shared memory			
			sm_max[threadIdx.x] = max(length(as_float3(force)), sspeed*sspeed/slength);
			# endif

			# ifdef XSPH_KERNEL
			xsph[index] = make_float4(2.0f*mean_vel, 0.0f);
			# endif

			forces[index] = force;
		}	// if (FLUID(info))
		else if (OBJECT(info)) {
			float4 force = make_float4(0.0f);

			// read other particle data from textures or global mem
			#if( __COMPUTE__ >= 20)
			const float4 pos = posArray[index];
			#else
			const float4 pos = tex1Dfetch(posTex, index);
			#endif

			// compute grid index of current particle
			const int3 gridPos = calcGridPosFromHash(particleHash[index]);

			// persistent variables across getNeibData calls
			char neib_cellnum = 0;
			uint neib_cell_base_index = 0;

			// loop over all neighbors
			for(uint i = 0; i < d_maxneibsnum_time_numparticles; i += numParticles) {
				neibdata neib_data = neibsList[i + index];

				if (neib_data == 0xffff) break;

				float3 pos_corr;
				const uint neib_index = getNeibIndex(pos, pos_corr, cellStart, neib_data, gridPos,
							neib_cellnum, neib_cell_base_index);

				// Compute relative position vector and distance
				// Now relPos is a float4 and neib mass is stored in relPos.w
				#if( __COMPUTE__ >= 20)
				const float4 relPos = pos_corr - posArray[neib_index];
				#else
				const float4 relPos = pos_corr - tex1Dfetch(posTex, neib_index);
				#endif
				const float r = length(as_float3(relPos));

				const particleinfo neib_info = tex1Dfetch(infoTex, neib_index);

				if (r < influenceradius) {
					float DvDt = 0.0;

					// Now object-object and object boundary forces are computed with ODE
					if (FLUID(neib_info) && !OBJECT(neib_info))
						DvDt = relPos.w*LJForce(r);

					as_float3(force) += DvDt*as_float3(relPos);
				}
			}

			uint rbindex = id(info) + d_rbstartindex[object(info)];
			rbforces[rbindex] = force;
			rbtorques[rbindex] = make_float4(cross(as_float3(pos) + gridPos*d_cellSize + 0.5f*d_cellSize
								- d_rbcg[object(info)], as_float3(force)));
		} // else if (OBJECT(info))
	}	// if index < numParticles)

	# ifdef DT_KERNEL
	dtadaptBlockReduce(sm_max, cfl);
	# endif
}
/************************************************************************************************************/

#undef FORCES_CUDA_KERNEL

/* vi:set ft=cuda: */
