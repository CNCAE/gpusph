/*  Copyright 2011-2013 Alexis Herault, Giuseppe Bilotta, Robert A. Dalrymple, Eugenio Rustico, Ciro Del Negro

    Istituto Nazionale di Geofisica e Vulcanologia
        Sezione di Catania, Catania, Italy

    Università di Catania, Catania, Italy

    Johns Hopkins University, Baltimore, MD

    This file is part of GPUSPH.

    GPUSPH is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    GPUSPH is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with GPUSPH.  If not, see <http://www.gnu.org/licenses/>.
*/

/* Euler kernel definitions */
// Predictor Corrector time integration
// - for step 1:
//	  v(n+1/2) = v(n) + f(n)*dt/2
//	  pos(n+1/2) = pos(n) + v(n)*dt/2
//
//	  We have oldVel = v(n), oldPos = pos(n), forces = f(n) so
//	  newVel = v(n+1/2) = oldVel + forces*dt/2
//	  newPos = pos(n+1/2) = oldPos + oldVel*dt/2
//
// - for step 2:
//	  vc(n+1/2) = v(n) + f(n+1/2)*dt/2
//	  posc(n+1/2) = pos(n) + vc(n+1/2)*dt/2
//	  then:
//	  v(n+1) = 2vc(n+1/2) - v(n) = v(n) + f(n+1/2)*dt
//	  pos(n+1) = 2posc(n+1/2) - pos(n) = pos(n) + vc(n+1/2)*dt
//
//	 Whe have oldVel = v(n), oldPos = pos(n), force = f(n+1/2),
//	  newVel = vel(n+1/2), newPos = pos(n+1/2) so
//	  we store velc = v(n) + f(n+1/2)*dt/2 then
//	  newPos = pos(n+1) = oldPos + velc*dt
//	  newVel = vel(n+1) = oldVel + forces*dt;

// Remember that for step 1 dt => dt/2 and for step 2 dt => dt !!!
// but dt2 is always equal to dt/2

template<int step, bool xsphcorr, BoundaryType boundarytype>
__global__ void
eulerDevice(
	const	float4	*oldPos,
	const	hashKey	*particleHash,
	const	float4	*oldVel,
	const	float4	*oldEulerVel,
	const	float4	*oldgGam,
	const	float	*oldTKE,
	const	float	*oldEps,
	const	particleinfo	*info,
	const	float4	*forces,
	const	float2	*contupd,
	const	float3	*keps_dkde,
	const	float4	*xsph,
			float4	*newPos,
			float4	*newVel,
			float4	*newEulerVel,
			float4	*newgGam,
			float	*newTKE,
			float	*newEps,
	// boundary elements are updated in-place, only used for rotation in the second step
			float4	*newBoundElement,
	const	uint	numParticles,
	const	float	full_dt,
	const	float	half_dt, /* full_dt/2 */
	const	float	t)
{
	const int index = INTMUL(blockIdx.x,blockDim.x) + threadIdx.x;

	if (index >= numParticles)
		return;

	// we use dt/2 on the first step, the actual dt on the second step
	const float dt = (step == 1) ? half_dt : full_dt;

	// read particle data from sorted arrays
	// Euler does nothing to boundary particles apart
	// copying pos and vel in the new arrays, except in DYN_BOUNDARY
	// case (boundarytype == DYN_BOUNDARY)
	float4 pos = oldPos[index];		// always pos(n)
	float4 vel = oldVel[index];		// always vel(n)
	float4 eulerVel = make_float4(0.0f);
	if (oldEulerVel)
		eulerVel = oldEulerVel[index];		// always eulerVel(n)
	// TODO templatize better
	float keps_k = oldTKE ? oldTKE[index] : NAN;	// always k(n)
	float keps_e = oldEps ? oldEps[index] : NAN;	// always e(n)

	const particleinfo pinfo = info[index];
	const ParticleType ptype = static_cast<ParticleType>(PART_TYPE(pinfo));

	do {
		// we only integrate ACTIVE particles which are not BOUNDARY particles,
		// unless dynbounds or sa_boundary is active (in which case BOUNDARY particles are also integrated)
		const bool integrateBoundary = (boundarytype == DYN_BOUNDARY || boundarytype == SA_BOUNDARY);
		if (!ACTIVE(pos) || (ptype == PT_BOUNDARY && !integrateBoundary))
			break;

		const float4 force = forces[index];	// f(n) at step 1 and f(n+1/2) at step 2
		// mean XSPH velocity, only when XSPH is active
		// the compiler is smart enough to actually optimize this out
		// in the xsphcorr == false case
		const float4 mean_vel = xsphcorr ? xsph[index] : make_float4(0);
		/*
		   velc = vel if step == 1, but
		   velc = vel + forces[index]*dt/2.0f if step == 2
		 */
		const float4 velc = vel + (step - 1)*force*half_dt;

		// object index
		const int obj = object(pinfo);

		// Updating particle position
		switch (ptype) {
		case PT_FLUID:
			// add weighted mean_vel only in xsphcorr case
			// the compiler is smart enough to optimize it out
			// in the xsphcorr == false case
			pos.x += (velc.x + xsphcorr*d_epsxsph*mean_vel.x)*dt;
			pos.y += (velc.y + xsphcorr*d_epsxsph*mean_vel.y)*dt;
			pos.z += (velc.z + xsphcorr*d_epsxsph*mean_vel.z)*dt;

			if (boundarytype == SA_BOUNDARY) {
				// Updating particle density
				// For step 1:
				//	  vel = vel(n+1/2) = vel(n) + f(n)*dt/2
				// For step 2:
				//	  vel = vel(n+1) = vel(n) + f(n+1/2)*dt
				// Improved continuity equation
				if (step == 1) {
					// gamma at time n+1 (gGam.w is gamma at time n)
					const float gamN = newgGam[index].w;
					const float gamNp1 = gamN + dt*contupd[index].y;
					vel.w = gamN/gamNp1*(vel.w + dt*force.w) + dt*contupd[index].x;
				}
				else {
					const float gamNp1o2 = newgGam[index].w;
					const float rhoNp1o2 = newVel[index].w;
					// gamma at time n (gGam.w is gamma at time n+1/2)
					const float gamN   = oldgGam[index].w;
					// gamma at time n+1
					const float gamNp1 = gamN + dt*contupd[index].y;
					vel.w = 1.0f/gamNp1*(gamN*vel.w + dt*vel.w/rhoNp1o2*gamNp1o2*force.w) + dt*contupd[index].x;
				}
				// Classical continuity equation
				//vel.w += dt*force.w + dt*contupd[index].x - dt*vel.w/gGam[index].w*contupd[index].y;
			}
			else
				vel.w += dt*force.w;
			// Fixed particles only evolve the density, non-fixed also evolve the velocity
			if (!FIXED_PART(pinfo)) {
				vel.x += dt*force.x;
				vel.y += dt*force.y;
				vel.z += dt*force.z;
			}

			// Updating k and e for k-e model
			if (keps_dkde) {
				const float3 dkde = keps_dkde[index];
				const float oldK = keps_k;
				// implicit formulations of k and e
				keps_k = (oldK   + dt*dkde.x)/(1.0f + dt*keps_e/oldK);
				keps_e = (keps_e + dt*dkde.y)/(1.0f + dt*keps_e/oldK*dkde.z);
			}
			break;
		// Updating velocity for vertex particles, used to set boundary conditions in k-e model
		case PT_BOUNDARY:
			if (boundarytype == DYN_BOUNDARY)
				vel.w += dt*force.w;
			if (boundarytype != SA_BOUNDARY)
				break;
			// SA_BOUNDARY: fall-through to VERTEX
		case PT_VERTEX:
			if (!IO_BOUNDARY(pinfo) || (VERTEX(pinfo) && CORNER(pinfo))) {
				// this is for k-eps only. Normally forces are going to be zero
				eulerVel += dt*force;
				// remove normal component of eulerian velocity
				const float4 gradGamma = newgGam[index];
				eulerVel -= dot3(gradGamma, eulerVel)/sqlength3(gradGamma)*gradGamma;
				// move segments and boundary particles if the MOVING flag is set
				if (MOVING(pinfo) && step == 2) {
					const uint i = object(pinfo)-1;
					// old position
					const float4 oldPos = pos;
					// Applying center of gravity translation
					pos.x += d_rbtrans[i].x;
					pos.y += d_rbtrans[i].y;
					pos.z += d_rbtrans[i].z;
					// Applying rotation around center of gravity
					const float3 relPos = d_worldOrigin + as_float3(pos) + calcGridPosFromParticleHash(particleHash[index])*d_cellSize + 0.5f*d_cellSize - d_rbcg[i];
					applyrot(&d_rbsteprot[9*i], relPos, pos);
					// compute velocity as position derivative
					vel.x = (pos.x - oldPos.x)/dt;
					vel.y = (pos.y - oldPos.y)/dt;
					vel.z = (pos.z - oldPos.z)/dt;
					// update normal of boundary element
					if (BOUNDARY(pinfo)) {
						const float3 normal = as_float3(newBoundElement[index]);
						applyrot(&d_rbsteprot[9*i], normal, newBoundElement[index]);
					}
				}
				else if (!MOVING(pinfo)) {
					vel.x = 0.0f;
					vel.y = 0.0f;
					vel.z = 0.0f;
				}
			}
			else {
				vel.x = 0.0f;
				vel.y = 0.0f;
				vel.z = 0.0f;
			}
			break;
		// Moving boundaries
		case PT_PISTON:
			vel.w += dt*force.w;
			// Updating positions for piston particles.
			// Now d_mbdata.x contains the piston velocity
			pos.x += d_mbdata[obj].x*dt;
			vel.x = d_mbdata[obj].x;
			break;
		case PT_PADDLE:
			{
			vel.w += dt*force.w;
			// Updating postions for paddle particles
			const float3 absPos = d_worldOrigin + as_float3(pos) + calcGridPosFromParticleHash(particleHash[index])*d_cellSize + 0.5f*d_cellSize;
			const float2 relPos = make_float2(absPos.x - d_mbdata[obj].x, absPos.z - d_mbdata[obj].y);
			const float c = cos(d_mbdata[obj].z*dt) - 1.0f;
			const float s = sin(d_mbdata[obj].z*dt);
			// Apply rotation around y axis
			pos.x += c*relPos.x + s*relPos.y;
			pos.z += -s*relPos.x + c*relPos.y;
			// Velocity
			vel.x = d_mbdata[obj].z*relPos.y;
			vel.z = -d_mbdata[obj].z*relPos.x;
			}
			break;
		case PT_GATE:
			vel.w += dt*force.w;
			// Updating positions for gate particles
			// mbdata.x,y,z contains gate velocity
			as_float3(pos) += as_float3(d_mbdata[obj])*dt;
			as_float3(vel) = as_float3(d_mbdata[obj]);
			break;
		case PT_OBJECT:
			vel.w += dt*force.w;
			if (step == 2) {
				// Applying center of gravity translation
				pos.x += d_rbtrans[obj].x;
				pos.y += d_rbtrans[obj].y;
				pos.z += d_rbtrans[obj].z;

				// Applying rotation around center of gravity
				const float3 relPos = d_worldOrigin + as_float3(pos) + calcGridPosFromParticleHash(particleHash[index])*d_cellSize + 0.5f*d_cellSize - d_rbcg[obj];
				applyrot(&d_rbsteprot[9*obj], relPos, pos);

				// Computing particles velocity
				// V(P) = V(Cg) + PCg^omega
				as_float3(vel) = d_rblinearvel[obj] + cross(d_rbangularvel[obj], relPos);
			}
			break;
		case PT_TESTPOINT:
			/* TODO */
			break;
		}
	} while (0);

	newPos[index] = pos;
	newVel[index] = vel;
	if (newEulerVel)
		newEulerVel[index] = eulerVel;
	if (newTKE)
		newTKE[index] = keps_k;
	if (newEps)
		newEps[index] = keps_e;
}

/* vi:set ft=cuda: */
